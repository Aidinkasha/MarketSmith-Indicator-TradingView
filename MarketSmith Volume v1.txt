// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© Fred6724

//@version=5
indicator("MarketSmith Volumes", format = format.volume)
prevC   = input(true, title='Color Based On Previous Close',  group="VOLUMES")
peakL   = input(9, "Peak Length", group="VOLUMES")
trunc   = input(false, title="Truncate for MarketSmith type display", group = "DISPLAY")
labels  = input(true, title="Labels on Volume", group = "DISPLAY")
lenDa   = input(50, "Length Daily", group = "MA")
lenWe   = input(10, "Length Weekly", group = "MA")
tf      = timeframe.period
ticker  = syminfo.tickerid

s       = ticker == "NSE:NIFTY" ? "NSE:NIFTY1!" : ticker == "NSE:BANKNIFTY" ? "NSE:BANKNIFTY1!" : ticker == "NSE:CNXFINANCE" ? "NSE:FINNIFTY1!" : ticker
vol     = request.security(s, tf, volume)
// Color of Volumes
colr = close>open ? color.rgb(39,54,233,0):color.rgb(222,50,174,0)
if(prevC)
    colr := close>close[1] ? color.rgb(39,54,233,0):color.rgb(222,50,174,0)
ma      = timeframe.isweekly ? ta.sma(vol, lenWe) : ta.sma(vol, lenDa)

// Display in the style of MarketSmith
// // Recalculation of the volume with limitation
vol2 = vol > 2*ma ? 2*ma:vol
// // Display

plot(trunc ? vol2:vol, title = "Volume" , color = colr, style = plot.style_histogram, linewidth = 3)
plot(ma, title = "MA", color = color.red)



//------------------  Marked Highs and Lows but for Volumes  ---------------------//

// Price Peak/Valley Points
// Highlights exact price at high or low points over a 19-period interval.
// For example, on a Daily chart, a High Price point is marked on the date
// where there has been no higher price the 9 days prior to that date and
// the 9 days following that date.

// High Price Point
if(not timeframe.isweekly and labels)
    pivotHigh      = ta.pivothigh(volume, peakL, peakL)
    if (pivotHigh)
        textvol9 = str.tostring(vol[peakL]/1000000, '0.0')+'M'
        if(vol >= 1000000000)
            textvol9 := str.tostring(vol[peakL]/1000000000, '0.0')+'B'
        volestvol = label.new(bar_index-peakL, trunc ? vol2[peakL]:vol[peakL], xloc=xloc.bar_index, yloc=yloc.price, style=label.style_none, text=textvol9, textcolor=color.rgb(0,0,0,0))
    
